/*******************************************************************************
 * Copyright (c) 2015 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtext.parser.fragments.GenerateFragmentTestLanguages

import org.eclipse.emf.mwe2.runtime.workflow.Workflow
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.*
import org.eclipse.xtext.generator.*

var projectName = "org.eclipse.xtext.tests"
var runtimeProject = "../${projectName}"
var lineDelimiter = '\n'

Workflow {
	bean = StandaloneSetup {
		platformUri = "${runtimeProject}/.."
	}
	
	component = DirectoryCleaner {
		directory = "${runtimeProject}/src-gen/org/eclipse/xtext/parser/fragments"
	}
	
	component = DirectoryCleaner {
		directory = "../../intellij/org.eclipse.xtext.core.idea.tests/src-gen/org/eclipse/xtext/parser/fragments/idea"
	}
	
	component = Generator auto-inject {
		pathRtProject = runtimeProject
		pathTestProject = runtimeProject
		projectNameRt = projectName
		
		language = {
			uri = "classpath:/org/eclipse/xtext/parser/fragments/FragmentTestLanguage.xtext"
			fragment = @TestLanguagesFragments auto-inject {
				srcGenOnly = true
			}
			fragment = junit.Junit4Fragment {}
		}
		language = {
			uri = "classpath:/org/eclipse/xtext/parser/fragments/FragmentTestLanguageEx.xtext"
			fragment = @TestLanguagesFragmentsEx auto-inject {
				srcGenOnly = true
			}
			fragment = junit.Junit4Fragment {}
		}
	}
}
