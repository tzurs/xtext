/*******************************************************************************
 * Copyright (c) 2015 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.parser.fragments.FragmentTestLanguage with org.eclipse.xtext.common.Terminals

generate fragmentTestLanguage "http://www.eclipse.org/2015/tmf/xtext/fragments"
 
ParserRuleFragments: {ParserRuleFragments} 
  ( '#1' element=PRFNamed
  | '#2' element=PRFNamed '->' ref=[PRFNamed]
  | '#3' element=PRFNamedRefFirst
  | '#4' element=PRFNamedWithAction
  | '#5' element=PRFNamedWithActionInFragment
  | '#6' element=PRFNamedWithActionInFragment2
  | '#7' element=PRFNamedWithActionInFragment3
  | '#8' element=PRFNamedWithFQN
  )
;

PRFNamed: PRFNamedFragment (
	  ':' ref=[PRFNamed]
	| '-' PRFNamedRef
	)?
;

PRFNamedRefFirst returns PRFNamed:
	ref=[PRFNamed] '<-' PRFNamedFragment
;

PRFNamedWithAction returns PRFNamed:
	PRFNamed {PRFNamedWithAction.prev=current} name=ID (ref=[PRFNamed] ref2=[PRFNamed])?
;

PRFNamedWithActionInFragment returns PRFNamed:
	FragmentWithAction ('-' ref=[PRFNamed])?
;

PRFNamedWithActionInFragment2 returns PRFNamed:
	name=ID FragmentWithAction2 ('-' ref=[PRFNamed])?
;

PRFNamedWithActionInFragment3 returns PRFNamed:
	FragmentWithAction3 ('-' ref=[PRFNamed])?
;

PRFNamedWithFQN returns PRFNamed:
	name=FQN ('-' ref=[PRFNamed|FQN2])?
;

FQN:
	ID Suffix? 
;

FQN2:
	ID Suffix2*
;

fragment Suffix:
	'.' ID Suffix?
;

fragment Suffix2:
	'.' ID
;

fragment FragmentWithAction returns PRFNamed:
	name=ID {PRFNamedWithAction.prev=current} name=ID (ref2=[PRFNamed])?
;

fragment FragmentWithAction2 returns PRFNamed:
	{PRFNamedWithAction.prev=current} name=ID (ref2=[PRFNamed])?
;

fragment FragmentWithAction3 returns PRFNamed:
	name=ID ({PRFNamedWithAction.prev=current} '->' name=ID (ref2=[PRFNamed])?)* 
;

fragment PRFNamedFragment returns PRFNamed:
	name=ID
;

fragment PRFNamedRef returns PRFNamed:
	ref=[PRFNamed]
;
